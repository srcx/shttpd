dnl Process this file with autoconf to produce a configure script.
AC_INIT(main.c)
AC_CONFIG_HEADER(aconfig.h)

dnl Disable pthreads by default ?
opt_pthreads=yes
AC_ARG_WITH(pthreads,
  [  --without-pthreads      disable pthreads support],
  test "x$withval" != "xyes" && opt_pthreads=no)

dnl Disable strict warnings ?
opt_strictwarn=yes
AC_ARG_ENABLE(strictwarn,
  [  --disable-strictwarn    disable strict compiler warnings],
  test "x$enableval" != "xyes" && opt_strictwarn=no)

dnl Debugging version ?
opt_debugging=no
AC_ARG_ENABLE(debugging,
  [  --enable-debugging      compile with debugging support],
  test "x$enableval" != "xno" && opt_debugging=yes)

dnl Checks for programs.
AC_PROG_CC
SHTTPD_PROG_LD_S

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_EXEEXT
AC_C_CONST

dnl Check for threads
has_pthreads=no
AC_MSG_CHECKING([whether to try to include pthreads support])
if test "x$opt_pthreads" = "xyes"; then
  AC_MSG_RESULT("yes")
  AC_CHECK_LIB(pthread, pthread_create, AC_DEFINE(PTHREADS) LIBS="$LIBS -lpthread"; has_pthreads=yes)
  if test "x$ac_cv_lib_pthread_pthread_create" != "xyes"; then
    AC_CHECK_FUNC(pthread_create, AC_DEFINE(PTHREADS) has_pthreads=yes)
  fi
else
  AC_MSG_RESULT("no")
fi

dnl Checks for networking support
AC_CHECK_FUNC(socket, check_for_libsocket=no, check_for_libsocket=yes)
if test "x$check_for_libsocket" = "xyes"; then
  AC_CHECK_LIB(socket, socket)
fi
AC_CHECK_FUNC(gethostbyaddr, check_for_libnsl=no, check_for_libnsl=yes)
if test "x$check_for_libnsl" = "xyes"; then
  AC_CHECK_LIB(nsl, gethostbyaddr)
fi

dnl Checks for library functions.
AC_FUNC_STRFTIME
if test "x$ac_cv_func_strftime" = "xyes"; then
  SHTTPD_HAVE_GNU_STRFTIME
fi
AC_CHECK_FUNCS(strdup stricmp strcasecmp)

dnl Final definitions
CFLAGS=
LDFLAGS=
AC_MSG_CHECKING([whether to compile with debugging support])
if test "x$opt_debugging" = "xyes"; then
  AC_MSG_RESULT("yes")
  if test "x$ac_cv_prog_cc_g" = "xyes"; then
    CFLAGS="$CFLAGS -g"
  fi
else
  AC_MSG_RESULT("no")
  CFLAGS="$CFLAGS -O3"
  if test "x$shttpd_cv_prog_ld_s" = "xyes"; then
    LDFLAGS="$LDFLAGS -s"
  fi
fi
AC_MSG_CHECKING([whether to produce strict compiler warnings])
if test "x$opt_strictwarn" = "xyes"; then
  AC_MSG_RESULT("yes")
  CFLAGS="$CFLAGS -Wall -Wno-unused -Werror"
else
  AC_MSG_RESULT("no")
  CFLAGS="$CFLAGS -Wall -Wno-unused"
fi
if test "x$has_pthreads" = "xyes"; then
  CFLAGS="$CFLAGS -D_REENTRANT"
fi
 
AC_OUTPUT(Makefile)
